# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>

_pkgname=media-player-info

pkgname=media-player-info-nosystemd
pkgver=22
pkgrel=2
pkgdesc="Data files describing media player capabilities, for post-HAL systems"
arch=('any')
license=('BSD')
url="http://cgit.freedesktop.org/media-player-info/"
groups=('elogind-nosystemd')
depends=('eudev')
makedepends=('python')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(http://www.freedesktop.org/software/media-player-info/$_pkgname-$pkgver.tar.gz)
sha256sums=('7ee7d7712834860533c46b16947238ef5b5d72f394fa7fb52783a15fba7b2336')
install=media-player-info.install

build() {
  cd $_pkgname-$pkgver

  ./configure --prefix=/usr \
      --with-udevdir=/usr/lib/udev
  LANG="en_US.UTF-8" make
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/licenses/$_pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$_pkgname"
}
