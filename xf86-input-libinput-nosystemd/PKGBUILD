# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

_pkgname=xf86-input-libinput

pkgname=xf86-input-libinput-nosystemd
pkgver=0.25.0
pkgrel=1
pkgdesc="Generic input driver for the X.Org server based on libinput"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
groups=("xorg-drivers-nosystemd")
depends=('libinput-nosystemd>=1.2.0')
makedepends=('xorg-server-devel-nosystemd' 'X-ABI-XINPUT_VERSION=24.1' 'libxi' 'libx11' 'resourceproto' 'scrnsaverproto')
provides=("$_pkgname=$pkgver")
conflicts=('xorg-server<1.19.0' 'X-ABI-XINPUT_VERSION<24' 'X-ABI-XINPUT_VERSION>=25' "$_pkgname")
#groups=('xorg-drivers')
source=(https://xorg.freedesktop.org/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2) #{,.sig})
sha256sums=('bb7fd1658fa7e6c757fd55480aa8a07f8ccfb0a80ae42c29dbc67d57958c556f')
# validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

build() {
  cd ${_pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --disable-static
  make
}

package() {
  cd ${_pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
