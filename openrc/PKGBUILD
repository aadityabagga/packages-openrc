# Maintainer: artoo <artoo@manjaro.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com/OpenRC/openrc/archive"

pkgname=openrc
pkgver=0.27.1
pkgrel=1
pkgdesc="Gentoo's universal init system"
arch=('i686' 'x86_64' 'armv7h')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
depends=('psmisc' 'pam')
#depends=('psmisc' 'pam' 'sysvinit')
optdepends=('netifrc: Network Interface Management Scripts'
            'networkmanager-openrc: NetworkManager'
            'connman-openrc: connman')
#conflicts=('systemd-sysvcompat')
conflicts=('systemd-sysvcompat' 'sysvinit')
replaces=('sysvinit')
install=${pkgname}.install
backup=('etc/rc.conf'
        'etc/conf.d/consolefont'
        'etc/conf.d/keymaps'
        'etc/conf.d/hostname'
        'etc/conf.d/modules'
        'etc/conf.d/hwclock'
        'etc/inittab')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${_url}/${pkgver}.tar.gz"
        "${pkgname}.logrotate"
        'openrc-install.hook'
        'openrc-remove.hook'
        mksysvinit1.patch::https://github.com/udeved/openrc/commit/a968fecf8928e44e288438fb170e2f94177617d8.patch
        mksysvinit2.patch::https://github.com/udeved/openrc/commit/705c918d2629187b221e7110dd6caefe60c1e704.patch)
sha256sums=('5589a7b56a16440c2c927c4d20740059e8c36064f094755e12f11540bf0578e9'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '1d4d31a7275660e7b23778e6fceb0714cab8ed2793b04aa004b01d8e7bcd43bd'
            'cbb4d00262a55a508114cb31e3903350be2cff748a4fb5e120dc745292e3987e'
            'c38c53ab147dfc867c49b891cd816e28358d615864fe4659ae72d4e8a1c7645e'
            'ca65baeed0d9a4430d802a0080944e6576e77eb585ccb03aabf06a50b8e78693')

_args=(SYSCONFDIR=/etc)
_args+=(PREFIX=/usr)
_args+=(SBINDIR=/usr/bin)
_args+=(LIBEXECDIR=/usr/lib/rc)
_args+=(MKSELINUX=no)
_args+=(MKPAM=pam)
_args+=(MKTERMCAP=ncurses)
_args+=(MKNET=no)
_args+=(MKSYSVINIT=yes)

if [ -f /etc/os-release ]; then
    . /etc/os-release
    _args+=(BRANDING="${NAME}")
else
    _args+=(BRANDING='Unknown Linux')
fi

prepare(){
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -e "s|/sbin|/usr/bin|g" -i support/sysvinit/inittab
    sed -i 's:0444:0644:' mk/sys.mk
    patch -Np 1 -i $srcdir/mksysvinit1.patch
    patch -Np 1 -i $srcdir/mksysvinit2.patch
}

build(){
    cd "${srcdir}/${pkgname}-${pkgver}"
    make "${_args[@]}"
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" "${_args[@]}" install

    install -m644 "${srcdir}/${pkgname}-${pkgver}/support/sysvinit/inittab" "${pkgdir}/etc/inittab"
    install -Dm644 "${srcdir}/${pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgname}"

    sed -e 's/#unicode="NO"/unicode="YES"/' \
        -e 's/#rc_logger="NO"/rc_logger="YES"/' \
        -i "${pkgdir}/etc/rc.conf"

    install -d ${pkgdir}/usr/lib/rc/cache

    install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

    for f in openrc-{install,remove}.hook;do
        install -Dm644 ${srcdir}/$f ${pkgdir}/usr/share/libalpm/hooks/$f
    done
}
